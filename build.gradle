buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.9' // declare before any other plugin

    id 'com.github.sherter.google-java-format' version '0.6'
    id 'com.github.johnrengelman.shadow' version '2.0.1' apply false
    id 'me.champeau.gradle.jmh' version '0.4.4' apply false
    id 'io.morethan.jmhreport' version '0.6.2.1' apply false
}

configurations {
    all*.exclude group: 'log4j', module: 'log4j'
}

repositories {
    jcenter()
}

buildScan { licenseAgreementUrl = 'https://gradle.com/terms-of-service'; licenseAgree = 'yes' }

googleJavaFormat {
    toolVersion = '1.4'
}

project(':auth') {
    description = 'Netifi Common Authentication and Authorization Components'
    ext.artifactName = 'netifi-sdk-auth'
}

project(':core') {
    description = 'Netifi SDK for JDK 1.8 and higher'
    ext.artifactName = 'netifi-sdk-java18'
}

project(':frames') {
    description = 'Netifi Common RSocket Frames'
    ext.artifactName = 'netifi-sdk-frames'
}

subprojects {
    apply plugin: 'java'
    apply from: file('../gradle/release.gradle')

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.compilerArgs << '-Xlint:all,-overloads,-rawtypes,-unchecked'
    }

    // custom tasks for creating source/javadoc jars
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
        baseName = "${artifactName}"
        version = "$releaseVersion"
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
        baseName = "${artifactName}"
        version = "$releaseVersion"
    }

    // add javadoc/source jar tasks as artifacts
    artifacts {
        archives sourcesJar, javadocJar
    }

    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/netifi/netifi-oss/' }
        mavenLocal()
    }

    dependencies {
        testCompile "junit:junit:4.12"
    }
}